plugins {
    id "fabric-loom" version "1.9-SNAPSHOT"
    id "me.modmuss50.mod-publish-plugin" version "0.8.3"
}

def isSnapshot = false
def mcVer = property("deps.minecraft_version")
if (mcVer.contains("-") || mcVer.contains("w")) {
    isSnapshot = true
    mcVer.replace("-", "")
}

version = project.mod_version + "+fabric" + "." + mcVer
group = project.maven_group

base {
    archivesName = project.mod_name
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven { url "https://maven.isxander.dev/releases" }
    mavenCentral()
}

loom {
    accessWidenerPath = file("../../src/main/resources/enhancedtooltips.accesswidener")
}

dependencies {
    minecraft "com.mojang:minecraft:${property("deps.minecraft_version")}"
    mappings "net.fabricmc:yarn:${property("deps.yarn_mappings")}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${property("deps.fapi_version")}"

    modImplementation "maven.modrinth:modmenu:${property("deps.modmenu_version")}"
    modImplementation "dev.isxander:yet-another-config-lib:${property("deps.yacl_version")}"
    include(modImplementation("blue.endless:jankson:${property("deps.jankson_version")}"))
}

if (stonecutter.current.isActive) {
    rootProject.tasks.register("buildActive") {
        group = "project"
        dependsOn(tasks.named("build"))
        dependsOn(tasks.named("publishMods"))
    }
}

processResources {
    var replaceProperties = [
            minecraft_range: project.property("deps.mc_range"),
            mod_id         : mod_id,
            mod_name       : mod_name,
            mod_license    : mod_license,
            mod_version    : project.version,
            mod_authors    : mod_authors,
            mod_description: mod_description
    ]
    replaceProperties.each { key, value -> inputs.property(key, value) }

    filesMatching("fabric.mod.json") {
        expand replaceProperties
    }
}

java {
    withSourcesJar()

    def java = stonecutter.eval(project.property("deps.minecraft_version"), ">=1.20.5")
            ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17

    sourceCompatibility = java
    targetCompatibility = java
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}

publishMods {
    if (mod_version.contains("alpha")) {
        type = ALPHA
    } else if (mod_version.contains("beta") || mod_version.contains("rc") || isSnapshot) {
        type = BETA
    } else {
        type = STABLE
    }

    changelog = "# ${project.version}\n" + file("../../CHANGELOG.md").text
    file = remapJar.archiveFile
    displayName = "EnhancedTooltips ${project.version}"

    modLoaders.addAll("fabric", "quilt")

    def mrOptions = modrinthOptions {
        projectId = "8H6RXl2q"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")

        requires("fabric-api")
        optional("yacl")

        // Discord
        announcementTitle = "Download from Modrinth"
    }

    def cfOptions = curseforgeOptions {
        projectId = "1173737"
        accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")

        requires("fabric-api")
        optional("yacl")

        // Discord
        announcementTitle = "Download from CurseForge"
        projectSlug = "enhancedtooltips"
    }

    if (project.property("deps.minecraft_version") == "1.21.1") {
        modrinth("m1.21.1") {
            from mrOptions

            minecraftVersionRange {
                start = "1.21"
                end = "1.21.1"
            }
        }

        curseforge("c1.21.1") {
            from cfOptions

            minecraftVersionRange {
                start = "1.21"
                end = "1.21.1"
            }
        }
    }

    if (project.property("deps.minecraft_version") == "1.21.3") {
        modrinth("m1.21.3") {
            from mrOptions

            minecraftVersionRange {
                start = "1.21.2"
                end = "1.21.3"
            }
        }

        curseforge("c1.21.3") {
            from cfOptions

            minecraftVersionRange {
                start = "1.21.2"
                end = "1.21.3"
            }
        }
    }

    if (project.property("deps.minecraft_version") == "1.21.4") {
        modrinth("m1.21.4") {
            from mrOptions

            minecraftVersions.add("1.21.4")
        }

        curseforge("c1.21.4") {
            from cfOptions

            minecraftVersions.add("1.21.4")
        }
    }

    github {
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        repository = "UltimatChamp/EnhancedTooltips"
        commitish = "main"

        // Discord
        announcementTitle = "Download from GitHub"
    }

    discord {
        webhookUrl = providers.environmentVariable("DISCORD_WEBHOOK")
        username = "EnhancedTooltips Releases"
        avatarUrl = "https://cdn.modrinth.com/data/8H6RXl2q/images/5eadf722db0fba952eadb08b8645c186c41e1507.png"

        style {
            look = "MODERN"
            thumbnailUrl = "https://cdn.modrinth.com/data/8H6RXl2q/images/e211c73de532ffc1d2c1e78e04d96ee2271dfa61.png"
        }
    }
}
